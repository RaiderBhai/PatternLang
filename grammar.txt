program      → decl_list stmt_list
decl_list    → declaration decl_list | ε
declaration  → var_decl ";" | func_decl
var_decl     → type ID ("=" expr)? 
             | "array" "<" type ">" ID "[" NUMBER "]"
type         → "int" | "bool" | "string"
func_decl    → "func" ID "(" param_list? ")" block
param_list   → type ID ("," type ID)*
stmt_list    → stmt stmt_list | ε
stmt         → var_decl ";" 
             | assign ";"
             | for_stmt
             | while_stmt
             | if_stmt
             | return_stmt ";"
             | print_stmt ";"
             | input_stmt ";"
             | newline_stmt ";"
             | block
assign       → ID "=" expr
             | ID "[" expr "]" "=" expr
for_stmt     → "for" ID "=" expr "to" expr block
while_stmt   → "while" "(" expr ")" block
if_stmt      → "if" "(" expr ")" block ("else" block)?
return_stmt  → "return" expr?
print_stmt   → "print" expr
input_stmt   → "input" ID
newline_stmt → "newline"
block        → "{" stmt_list "}"
expr         → logic_or
logic_or     → logic_and ( "||" logic_and )*
logic_and    → equality ( "&&" equality )*
equality     → rel (("=="|"!=") rel)*
rel          → add (("<"|">"|"<="|">=") add)*
add          → mul (("+"|"-") mul)*
mul          → unary (("*"|"/"|"%") unary)*
unary        → ("!"|"-") unary | primary
primary      → NUMBER | STRING | BOOL | ID | func_call | "(" expr ")"
func_call    → ID "(" arg_list? ")"
arg_list     → expr ("," expr)*